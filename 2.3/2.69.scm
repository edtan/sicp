#lang sicp
;Exercise 2.69.  The following procedure takes as its argument a list of symbol-frequency pairs (where no symbol appears in more than one pair) and generates a Huffman encoding tree according to the Huffman algorithm.

;(define (generate-huffman-tree pairs)
;  (successive-merge (make-leaf-set pairs)))

;Make-leaf-set is the procedure given above that transforms the list of pairs into an ordered set of leaves. Successive-merge is the procedure you must write, using make-code-tree to successively merge the smallest-weight elements of the set until there is only one element left, which is the desired Huffman tree. (This procedure is slightly tricky, but not really complicated. If you find yourself designing a complex procedure, then you are almost certainly doing something wrong. You can take significant advantage of the fact that we are using an ordered set representation.)

(define (make-leaf symbol weight)
  (list 'leaf symbol weight))
(define (leaf? object)
  (eq? (car object) 'leaf))
(define (symbol-leaf x) (cadr x))
(define (weight-leaf x) (caddr x))

(define (make-code-tree left right)
  (list left
        right
        (append (symbols left) (symbols right))
        (+ (weight left) (weight right))))
(define (left-branch tree) (car tree))
(define (right-branch tree) (cadr tree))
(define (symbols tree)
  (if (leaf? tree)
      (list (symbol-leaf tree))
      (caddr tree)))
(define (weight tree)
  (if (leaf? tree)
      (weight-leaf tree)
      (cadddr tree)))

(define (decode bits tree)
  (define (decode-1 bits current-branch)
    (if (null? bits)
        '()
        (let ((next-branch
               (choose-branch (car bits) current-branch)))
          (if (leaf? next-branch)
              (cons (symbol-leaf next-branch)
                    (decode-1 (cdr bits) tree))
              (decode-1 (cdr bits) next-branch)))))
  (decode-1 bits tree))
(define (choose-branch bit branch)
  (cond ((= bit 0) (left-branch branch))
        ((= bit 1) (right-branch branch))
        (else (error "bad bit -- CHOOSE-BRANCH" bit))))

(define (element-of-set? x set)
  (cond ((null? set) false)
        ((eq? x (car set)) true)
        (else (element-of-set? x (cdr set)))))
(define (encode message tree)
  (if (null? message)
      '()
      (append (encode-symbol (car message) tree)
              (encode (cdr message) tree))))

(define (encode-symbol symbol tree)
  (cond ((leaf? tree) '())
        ((element-of-set? symbol (symbols (left-branch tree))) (cons 0 (encode-symbol symbol (left-branch tree))))
        ((element-of-set? symbol (symbols (right-branch tree))) (cons 1 (encode-symbol symbol (right-branch tree))))
        (else (error "ERR: get-next-branch: symbol was not found in tree!" symbol))))


(define (adjoin-set x set)
  (cond ((null? set) (list x))
        ((< (weight x) (weight (car set))) (cons x set))
        (else (cons (car set)
                    (adjoin-set x (cdr set))))))
(define (make-leaf-set pairs)
  (if (null? pairs)
      '()
      (let ((pair (car pairs)))
        (adjoin-set (make-leaf (car pair)    ; symbol
                               (cadr pair))  ; frequency
                    (make-leaf-set (cdr pairs))))))

;(display (make-leaf-set '((A 4) (B 2) (C 1) (D 1))))
;((leaf D 1) (leaf C 1) (leaf B 2) (leaf A 4))

(define (generate-huffman-tree pairs)
  (successive-merge (make-leaf-set pairs)))

(define (successive-merge nodes)
  (let ((first-node (car nodes)))
    ;(display nodes)
    ;(newline)
    (cond ((null? nodes) '())
          ((null? (cdr nodes)) first-node)
          (else (let ((second-node (cadr nodes)))
                  (let ((merged-node (make-code-tree first-node second-node)))
                    (let ((tree-without-first-two (cddr nodes)))
                      (let ((new-tree (adjoin-set merged-node tree-without-first-two)))
                        (successive-merge new-tree)))))))))
         
(display (generate-huffman-tree '((E 3) (A 4) (B 2) (C 1) (D 1))))
(newline)
;((leaf A 4) ((leaf B 2) ((leaf D 1) (leaf C 1) (D C) 2) (B D C) 4) (A B D C) 8)(((leaf B 2) ((leaf D 1) (leaf C 1) (D C) 2) (B D C) 4) ((leaf E 3) (leaf A 4) (E A) 7) (B D C E A) 11)                 
(display (generate-huffman-tree '((A 4) (B 2) (C 1) (D 1))))
(newline)
;((leaf A 4) ((leaf B 2) ((leaf D 1) (leaf C 1) (D C) 2) (B D C) 4) (A B D C) 8)

(define my-tree (generate-huffman-tree '((A 4) (B 2) (C 1) (D 1))))
(define my-message '(B A C D A B B A))
(display (encode my-message my-tree))
(newline)
;(1 0 0 1 1 1 1 1 0 0 1 0 1 0 0)
(display (decode '(1 0 0 1 1 1 1 1 0 0 1 0 1 0 0) my-tree))
(newline)
;(B A C D A B B A)